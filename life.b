#!/usr/bin/bf
>++++[-<++++>>++++<]>[-<++++>]<<[>>
# получаем 16 и 64 и начинаем цикл
  >>++++++[<++++++++>-]>>>+<<<,
  # получаем 48 и вводим символ
  <[>[-<]>]>>[-<<]<[->+>>>+<<<<]>[-<+>]<
  # вычитаем из символа 48 до нуля и доублируем результат
  <[-]<++++[->++++<]>>>>>[-]+<<<
  # записываем 16 и один
  <[>[-<]>]>>[-<<]<
  # вычитаем 16 до нуля из первой копии
  <[-]+>>>>[-]<<<
  # удаляем все лишнее
  [>>>>[-]<<<<[->+>>>+<<<<]>[-<+>]<
  # если была введена буква то удаляем копию и заново дублируем
    >>>+<<<<+++++[>[-<]>]>>[-<<]<
    # вычитаем 6 до нуля
  <[-]+>[>>>>[-]<<<<<->[-]]<[>>>>>+++++++++<<<<<-]>]
  # если результат больше 0 то обнуляем копию иначе прибавим 9 к копии
  <[+++++++++>>>>>[-<+<<<+>>>>]<[->+<]+<<<
  # если была введена цифра то записываем 10 восстанавливаем цифру и ставим 1
    <[>[-<]>]>>[-<<]<
    # вычитаем 10 до нуля
  [>>>>[-]<<<<[-]]<[-]]
  # если результат больше 0 то обнуляем копию
  <<[->>>+<<<]>>>>>>[-]<<
  # удаляем все лишенее
  ++++[-<[-<+>>>>>>>>>>>+<<<<<<<<<<]<[->+<]>>[>+>+<<-]>>[-<<+>>]
  # устанавливаем на 4 двоичных числа потом дублируем
  +<[>[>>++<<-]>>[<<+>>-]<<<-]
  # возводим в степень
    >->[->+>>>+<<<<]>[-<+>]>>+<<<
    # дублируем исходное число и устанавливаем 1
    <[>[-<]>]>>[-<<]<<[-]+>
    # вычитаем степень до нуля и ставим 1 для условия
    [<->->>>->[-]>+++<<<<<[->>>>+<<<<]] # 1=3
    # нужный бит установлен значит сохраним результат вычитания и выведем символ
    <[->>>>>>++<<<<<<] # 0=2
    # бит не установлен значит выведем другой символ
  >>>>>[<<<<+>>>>-]>[->>+<<]>[->[->+<]<[->+<]+>]<[-<]<<[-]<<<<<<<->]
  # сдвигаем результат вычитания на место
<[-<<<+>>>]<<<<-]
# заканчиваем цикл
# массив располагается с $10 по $4F ячейки перед ним ничего нет
# начнем вывод массива
>>>>>>>>>>>>>>>>[[-<<<<<<+>>>>>>]>]<<<
# сдвинули массив на 5 к началу и перешли к концу массива
+++++[-<+++++++++<+++++++>>]++++++++
# получили символы для вывода и 8
<<<<[>+<--[->->.>>>>+++<<<<<<]>[->>.>>>++<<<<<]>[[-<+>]>]
# для каждого элемента массива поставили за ним 1 вычли 2 проверили на 0 вывели нужный символ сдвинулись
<+<-[>-<[->>+<<]]>[->++++++++++.--<]>[-<<+>>]<<<<<<]
в нужных местах начинаем новую строку
>>[[-]>]
# убираем лишнее
# 1234567890ABCDEF

